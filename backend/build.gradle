plugins {
	id 'java'
	id 'org.springframework.boot' version '3.5.7'
	id 'io.spring.dependency-management' version '1.1.7'
	id 'com.epages.restdocs-api-spec' version "0.18.2"
}

group = 'com.rejs'
version = '0.0.1-SNAPSHOT'
description = '동시성 문제해결을 위한 가상 수강신청시스템'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
	maven{
		url 'https://jitpack.io'
	}
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	compileOnly 'org.projectlombok:lombok'
	runtimeOnly 'org.mariadb.jdbc:mariadb-java-client'
	annotationProcessor 'org.projectlombok:lombok'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.boot:spring-boot-testcontainers'
	testImplementation 'org.testcontainers:junit-jupiter'
	testImplementation 'org.testcontainers:mariadb'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

	// security
	implementation 'org.springframework.boot:spring-boot-starter-security'
	testImplementation 'org.springframework.security:spring-security-test'

	// springdoc 연계를 위해
	implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.6.0'

	// mockMvc를 사용하므로
	testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc'
	testImplementation 'com.epages:restdocs-api-spec-mockmvc:0.18.2'

	// actuator
	implementation 'org.springframework.boot:spring-boot-starter-actuator'

	// OpenTelemetry Bridge (Brave 대신 사용)
	implementation 'io.micrometer:micrometer-tracing-bridge-otel'

	// OTLP Exporter (Tempo로 데이터를 쏘는 역할)
	implementation 'io.opentelemetry:opentelemetry-exporter-otlp'

	// aop
	implementation 'org.springframework.boot:spring-boot-starter-aop:3.5.9'

	// jdbc에 대한 trace 적용
	implementation 'net.ttddyy.observation:datasource-micrometer-spring-boot:1.2.1'

	// loki
	implementation 'com.github.loki4j:loki-logback-appender:1.5.1'
	implementation 'net.logstash.logback:logstash-logback-encoder:7.4'

	// prometheus
	implementation 'io.micrometer:micrometer-registry-prometheus'

	// jwt 인증을 위한 resource server
	// resource server는 public key 바탕의 커스텀 JWT 인증을 지원한다. 놀랍게도.
	// 발급기능은 커스텀으로 구현해야한다
	implementation 'org.springframework.boot:spring-boot-starter-oauth2-resource-server'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

tasks.named('test') {
	useJUnitPlatform()
}

openapi3{
	servers = [
			{
				url = 'http://localhost:8080/api';
				description="개발서버"
			},
			{
				url = 'http://localhost:8081/api';
				description="테스트 통합 서버"
			},
			{
				url = 'http://localhost:8082/api';
				description="테스트 서버1"
			},
			{
				url = 'http://localhost:8083/api';
				description="테스트 서버2"
			},
	]
	title = "registration"
	description = "수강신청시스템"
	version="1.0.0"
	format = "yaml"
}

tasks.register('docs', Copy) {
	doFirst {
		delete file('src/main/resources/docs/openapi3.yaml')
	}
	from file('build/api-spec/openapi3.yaml')
	into file('src/main/resources/docs/.')
	dependsOn tasks.named('openapi3')

	dependsOn tasks.named('processResources')
	doLast{
		delete file('build/generated-snippets')
	}
}

postman {
	title = 'Registration'
	version = '1.0.0'
	baseUrl = 'https://localhost:8080/api'
	separatePublicApi = true
	outputFileNamePrefix = 'registration-postman-collection'
}
